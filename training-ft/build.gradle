plugins {
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id "io.freefair.lombok"
    id 'java'
}

ext {
    cucumberVersion = '7.8.0'
    jacksonDataBinding = '2.13.4'
    wiremockStandalone = '2.34.0'
    googleGuavaVersion = '31.1-jre'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation project(":training-app")

    testImplementation "io.cucumber:cucumber-spring:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"

    testImplementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDataBinding}"

    testImplementation('com.datastax.oss.simulacron:simulacron-driver-3x:0.11.0') {
        exclude group: 'com.datastax.cassandra'
    }

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.google.guava:guava:31.1-jre"
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber {
    def profile = findProperty("spring.profiles.active") ?: "local"
    dependsOn processTestResources, compileTestJava

    doLast {
        javaexec {
            systemProperties = ['spring.profiles.active': profile]
            main = "io.cucumber.core.cli.Main"
            classpath = project.sourceSets.test.runtimeClasspath
            args = ['--tags', 'not @ignore',
                    '--plugin', 'pretty',
                    '--plugin', 'html:reports/report.html',
                    '--glue', 'com.generoso.ft.training.simulacron',
                    'classpath:features']
        }
    }
}

bootJar.enabled = false
jar.enabled = false